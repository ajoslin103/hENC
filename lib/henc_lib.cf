body copy_from digest_cp(from)
# digest copy from policy hub
{
      servers     => { "$(sys.policy_hub)" } ;
      source      => "$(from)" ;
      compare     => "digest" ;
      copy_backup => "false" ;

  trust_servers_key::
      trustkey    => "true" ;
}


body perms root_executable
# equivalent to mog("0755","root","root")
{
          owners => { "root" };
          groups => { "root" };
          mode   => "0755";
}


bundle common henc_cfg
# configuration for bundle agent henc; edit the variables in this file
# to suit your needs
{
  vars:
    any::
      "local_modules"
	  comment => "Local directory for modules",
	  string  => "$(def.dir_modules)" ;

      "master_modules"
	  comment => "Source directory for modules",
	  string  => translatepath("$(def.dir_masterfiles)/modules") ;

      "master_enc"
	  comment => "Base directory for ENC files",
	  string  => "$(def.dir_masterfiles)" ;

    is_running_henc_tests::
      "local_enc"
	  comment => "Local directory for caching ENC files",
	  string  => "." ;

    hasnt_sys_inputdir::
      "local_enc"
	  comment => "Local directory for caching ENC files",
	  string  => translatepath("$(sys.workdir)/inputs") ;

    has_sys_inputdir::
      "local_enc"
	  comment => "Local directory for caching ENC files",
	  string  => "$(sys.inputdir)" ;


  classes:
    !is_running_henc_tests::
      "has_sys_inputdir"
	  expression => isvariable("sys.inputdir") ;

      "hasnt_sys_inputdir"
	  not => "has_sys_inputdir" ;
}
